# ERC1155
ERC1155是一种新颖的代币标准，旨在从以前的标准中吸取精华，创建一个不考虑*可替代性*和gas效率的*代币合约*。

> TIP
ERC1155从*ERC20*、*ERC721*和[ERC777](https://eips.ethereum.org/EIPS/eip-777)中吸取了想法。如果你对这些标准不熟悉，可以先看看他们的指南。

## 多代币标准
ERC1155的独特之处在于它使用一个智能合约同时表示多个代币。这就是为什么它的*balanceOf*函数与ERC20和ERC777的不同：它有一个额外的id参数，用于查询你想要查询余额的代币的标识符。

这与ERC721的做法类似，但在那个标准中，代币id没有余额的概念：每个代币都是不可替代的，存在或不存在。ERC721的*balanceOf*函数指的是一个账户拥有多少不同的代币，而不是每个代币有多少。另一方面，在ERC1155中，账户对每个代币id都有一个明确的余额，而不可替代的代币只是通过简单地铸造其中的一个来实现的。

这种方法为需要多种代币的项目带来了巨大的燃气节省。不需要为每种代币类型部署一个新的合约，一个ERC1155代币合约就可以保存整个系统状态，降低部署成本和复杂性。

## 批量操作
因为所有的状态都保存在一个合约中，所以可以非常有效地在一次交易中操作多个代币。该标准提供了两个函数，*balanceOfBatch*和*safeBatchTransferFrom*，使查询多个余额和转移多个代币变得更简单和更少的燃气消耗。

为了符合标准，我们还在非标准函数中包括了批量操作，如*_mintBatch*。

## 构建一个ERC1155代币合约
我们将使用ERC1155来跟踪我们游戏中的多个项目，每个项目都有自己独特的属性。我们将所有项目铸造给合约的部署者，后来我们可以将它们转移到玩家手中。玩家可以自由地保留他们的代币，或者像他们在区块链上处理任何其他资产一样，与其他人交易。

为了简单起见，我们将在构造函数中铸造所有项目，但你可以在合约中添加铸造功能，以便按需向玩家铸造。

> TIP
关于铸造机制的概述，请查看*创建ERC20供应*。

以下是一个代币化项目的合约可能是什么样子的示例:
```
// contracts/GameItems.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import {ERC1155} from "@openzeppelin/contracts/token/ERC1155/ERC1155.sol";

contract GameItems is ERC1155 {
    uint256 public constant GOLD = 0;
    uint256 public constant SILVER = 1;
    uint256 public constant THORS_HAMMER = 2;
    uint256 public constant SWORD = 3;
    uint256 public constant SHIELD = 4;

    constructor() ERC1155("https://game.example/api/item/{id}.json") {
        _mint(msg.sender, GOLD, 10**18, "");
        _mint(msg.sender, SILVER, 10**27, "");
        _mint(msg.sender, THORS_HAMMER, 1, "");
        _mint(msg.sender, SWORD, 10**9, "");
        _mint(msg.sender, SHIELD, 10**9, "");
    }
}
```

请注意，在我们的游戏物品中，金币是可替代的代币，而雷神之锤是不可替代的代币，因为我们只铸造了一个。

*ERC1155*合约包含了可选的扩展*IERC1155MetadataURI*。这就是*uri*函数的来源：我们用它来检索元数据uri。

另外请注意，与ERC20不同，ERC1155没有小数字段，因为每个代币都是独一无二的，不能被分割。

一旦部署，我们将能够查询部署者的余额。
```
> gameItems.balanceOf(deployerAddress,3)
1000000000
```

我们可以将物品转移到玩家账户上:
```
> gameItems.safeTransferFrom(deployerAddress, playerAddress, 2, 1, "0x0")
> gameItems.balanceOf(playerAddress, 2)
1
> gameItems.balanceOf(deployerAddress, 2)
0
```

我们也可以批量转移物品到玩家账户，并获取批次的余额:
```
> gameItems.safeBatchTransferFrom(deployerAddress, playerAddress, [0,1,3,4], [50,100,1,1], "0x0")
> gameItems.balanceOfBatch([playerAddress,playerAddress,playerAddress,playerAddress,playerAddress], [0,1,2,3,4])
[50,100,1,1,1]
```

元数据URI可以获取:
```
> gameItems.uri(2)
"https://game.example/api/item/{id}.json"
```

URI可以包含字符串{id}，客户端必须用实际的令牌ID替换它，以小写十六进制表示（没有0x前缀），并用零填充到64个十六进制字符。

对于令牌ID 2和uri https://game.example/api/item/{id}.json，客户端会将{id}替换为0000000000000000000000000000000000000000000000000000000000000002，以在https://game.example/api/item/0000000000000000000000000000000000000000000000000000000000000002.json处检索JSON。

令牌ID 2的JSON文档可能看起来像这样:
```
{
    "name": "Thor's hammer",
    "description": "Mjölnir, the legendary hammer of the Norse god of thunder.",
    "image": "https://game.example/item-id-8u5h2m.png",
    "strength": 20
}
```

关于元数据JSON模式的更多信息，请查看[ERC-1155元数据URI JSON模式](https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1155.md#erc-1155-metadata-uri-json-schema)。

> NOTE
你会注意到，物品的信息包含在元数据中，但这些信息并不在链上！因此，游戏开发者可以更改底层的元数据，改变游戏的规则！
> TIP
如果你想将所有物品信息放在链上，你可以通过提供一个带有JSON模式编码的*Base64*数据URI来扩展ERC721来实现这一点（尽管这将相当昂贵）。你也可以利用IPFS来存储URI信息，但这些技术超出了本概述指南的范围。

## 向合约发送代币
使用*safeTransferFrom*时的一个关键区别是，向其他合约转移代币可能会出现以下消息的回滚:
```
ERC1155: transfer to non ERC1155Receiver implementer
```

这是一件好事！这意味着接收合约没有将自己注册为了解ERC1155协议，因此为了**防止代币被永久锁定**，向其转账被禁用。例如，[Golem合约目前持有超过350k GNT代币](https://etherscan.io/token/0xa74476443119A942dE498590Fe1f2454d7D4aC0d?a=0xa74476443119A942dE498590Fe1f2454d7D4aC0d)，价值数万美元，并且缺乏将它们取出的方法。这几乎发生在每一个ERC20支持的项目上，通常是由于用户错误。

为了让我们的合约接收ERC1155代币，我们可以继承便利合约*ERC1155Holder*，它为我们处理注册。但是我们需要记住实现功能，以允许代币从我们的合约中转出。
```
// contracts/MyContract.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import {ERC1155Holder} from "@openzeppelin/contracts/token/ERC1155/utils/ERC1155Holder.sol";

contract MyContract is ERC1155Holder {
}
```
我们还可以使用*onERC1155Received*和*onERC1155BatchReceived*函数实现更复杂的场景。