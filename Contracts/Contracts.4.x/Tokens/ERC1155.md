# ERC1155
ERC1155是一种新颖的代币标准，旨在从以前的标准中汲取最佳内容，创建一个*不考虑同质性的*、**[高效的代币合约](./Tokens.md)**。

> TIP
ERC1155从[ERC20](./ERC20/ERC20.md)、[ERC721](./ERC721.md)和[ERC777](./ERC777.md)中汲取了想法。如果您对这些标准不熟悉，请先查看它们的指南。

## 多代币标准
ERC1155的独特之处在于它使用一个智能合约同时表示多个代币。这就是它的[balanceOf](../API/ERC1155.md)函数与ERC20和ERC777不同的原因：它有一个额外的id参数，用于查询您想要查询余额的代币的标识符。

这与ERC721的做法类似，但在该标准中，代币id没有余额的概念：每个代币都是不可互换的，存在或不存在。ERC721的[balanceOf](../API/ERC721.md)函数是指一个帐户拥有多少不同的代币，而不是每个代币有多少。另一方面，在ERC1155中，帐户对于每个代币id都有一个不同的余额，而非互换代币则通过简单地铸造其中的一个来实现。

这种方法为需要多个代币的项目节省了大量的gas。与为每个代币类型部署一个新合约不同，一个单独的ERC1155代币合约可以保有整个系统状态，降低部署成本和复杂度。

## 批量操作
由于所有状态都保存在单个合约中，因此可以在单个交易中非常高效地操作多个代币。标准提供了两个函数，[balanceOfBatch和safeBatchTransferFrom](../API/ERC1155.md)，使查询多个余额和转移多个代币变得更简单、更节省gas。

我们还在非标准函数中包括了批量操作，例如*_mintBatch*。

## 构建ERC1155代币合约
我们将使用ERC1155来跟踪我们项目中的多个物品，它们每个都具有自己独特的属性。我们将所有物品铸造给合约的部署者，然后可以将它们转移给玩家。玩家可以自由地保留他们的代币或与其他人交易，就像在区块链上交易任何其他资产一样！

为简单起见，我们将在构造函数中铸造所有物品，但您可以向合约添加铸造功能，以按需向玩家铸造。

> TIP
有关铸造机制的概述，请查看*创建ERC20供应*。

下面是一个代币化物品的合约示例：
```
// contracts/GameItems.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC1155/ERC1155.sol";

contract GameItems is ERC1155 {
    uint256 public constant GOLD = 0;
    uint256 public constant SILVER = 1;
    uint256 public constant THORS_HAMMER = 2;
    uint256 public constant SWORD = 3;
    uint256 public constant SHIELD = 4;

    constructor() ERC1155("https://game.example/api/item/{id}.json") {
        _mint(msg.sender, GOLD, 10**18, "");
        _mint(msg.sender, SILVER, 10**27, "");
        _mint(msg.sender, THORS_HAMMER, 1, "");
        _mint(msg.sender, SWORD, 10**9, "");
        _mint(msg.sender, SHIELD, 10**9, "");
    }
}
```
请注意，对于我们的项目物品，金币是可替代的代币，而索尔的锤子是不可替代的代币，因为我们只铸造了一个。

*ERC1155*合约包括可选扩展*IERC1155MetadataURI*。这就是*uri*函数的来源：我们使用它来检索元数据uri。

还要注意，与ERC20不同，ERC1155缺乏小数位字段，因为每个代币都是独特的，不能被分割。

一旦部署，我们将能够查询部署者的余额：
```
> gameItems.balanceOf(deployerAddress,3)
1000000000
```

我们可以将物品转移到玩家账户中：
```
> gameItems.safeTransferFrom(deployerAddress, playerAddress, 2, 1, "0x0")
> gameItems.balanceOf(playerAddress, 2)
1
> gameItems.balanceOf(deployerAddress, 2)
0
```

我们还可以批量将物品转移至玩家账户，并获得批次的余额。
```
> gameItems.safeBatchTransferFrom(deployerAddress, playerAddress, [0,1,3,4], [50,100,1,1], "0x0")
> gameItems.balanceOfBatch([playerAddress,playerAddress,playerAddress,playerAddress,playerAddress], [0,1,2,3,4])
[50,100,1,1,1]
```
元数据URI可以获得：
```
> gameItems.uri(2)
"https://game.example/api/item/{id}.json"
```

URI可以包含字符串{id}，客户端必须用实际的代币ID替换它，以小写十六进制表示（不带0x前缀），并在64个十六进制字符前面填充零。

对于代币ID 2和URI https://game.example/api/item/{id}.json，客户端将使用0000000000000000000000000000000000000000000000000000000000000002替换{id}，以检索位于https://game.example/api/item/0000000000000000000000000000000000000000000000000000000000000002.json的JSON。

代币ID 2的JSON文档可能如下所示：
```
{
    "name": "Thor's hammer",
    "description": "Mjölnir, the legendary hammer of the Norse god of thunder.",
    "image": "https://game.example/item-id-8u5h2m.png",
    "strength": 20
}
```
有关元数据 JSON 模式的更多信息，请查看 [ERC-1155 元数据 URI JSON 模式](https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1155.md#erc-1155-metadata-uri-json-schema)。

> NOTE
您会注意到，物品的信息包含在元数据中，但该信息不在链上！因此，项目开发人员可以更改底层元数据，从而改变项目规则！

> TIP
如果您想将所有物品信息放在链上，可以通过提供带有编码的 JSON 模式的 *Base64* 数据 URI 来扩展 ERC721（尽管这将非常昂贵）。您也可以利用 IPFS 存储 URI 信息，但这些技术超出了本概述指南的范围。

## 向合约发送代币
使用 *safeTransferFrom* 时的一个关键区别是，将代币转移到其他合约可能会导致以下消息回滚：
```
ERC1155: transfer to non ERC1155Receiver implementer
```
这是一件好事！这意味着接收方合约没有注册为支持ERC1155协议，因此转移给它的代币被禁用，以**防止代币被永久锁定**。例如，[Golem合约目前持有超过350k GNT代币](https://etherscan.io/token/0xa74476443119A942dE498590Fe1f2454d7D4aC0d?a=0xa74476443119A942dE498590Fe1f2454d7D4aC0d)，价值数万美元，缺乏将它们取出的方法。这在几乎所有基于ERC20的项目中都发生过，通常是由于用户错误。

为了使我们的合约能够接收ERC1155代币，我们可以继承方便的合约*ERC1155Holder*，它会为我们处理注册。不过我们需要记住实现功能，允许代币从我们的合约中转移出去：
```
// contracts/MyContract.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC1155/utils/ERC1155Holder.sol";

contract MyContract is ERC1155Holder {
}
```
我们还可以使用*onERC1155Received*和*onERC1155BatchReceived*函数实现更复杂的场景。

## 预设ERC1155合约
有一个预设的ERC1155合约，[ERC1155PresetMinterPauser](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/release-v4.7/contracts/token/ERC1155/presets/ERC1155PresetMinterPauser.sol)。它预设允许代币铸造（创建） - 包括批量铸造，停止所有代币转移（暂停）并允许持有人烧毁（销毁）其代币。该合约使用*访问控制*来控制对铸造和暂停功能的访问。部署合约的账户将被授予铸造者和暂停者角色，以及默认的管理员角色。

该合约可以直接部署而无需编写任何Solidity代码。它可用于快速原型设计和测试，但也适用于生产环境。

> NOTE
合约预设现已被废弃，取而代之的是[Contracts Wizard](https://wizard.openzeppelin.com/)作为一种更强大的替代方案。