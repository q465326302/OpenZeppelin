# ERC 1155
ERC1155是一种新颖的代币标准，旨在吸取以前标准的优点，创建一个*对可互换性不敏感*且**燃气高效**的*代币合约*。

> TIP
ERC1155从*ERC20*、*ERC721*和*ERC777*中汲取了一些思想。如果您对这些标准不熟悉，请在继续之前查看它们的指南。

## 多代币标准
ERC1155的独特之处在于它使用单个智能合约同时代表多个代币。这就是为什么它的*balanceOf*函数与ERC20和ERC777不同的原因：它有一个额外的id参数，用于查询您想要查询余额的代币的标识符。

这类似于ERC721的做法，但在该标准中，代币id没有余额的概念：每个代币都是不可互换的，存在或不存在。ERC721的*balanceOf*函数指的是一个帐户拥有多少个不同的代币，而不是每个代币有多少个。另一方面，在ERC1155中，帐户对每个代币id都有一个独特的余额，并且通过简单地铸造其中一个来实现非可互换代币。

这种方法为需要多个代币的项目节省了大量的燃气。不需要为每种代币类型部署一个新的合约，一个单一的ERC1155代币合约可以保存整个系统状态，减少部署成本和复杂性。

## 批量操作
由于所有状态都保存在单个合约中，可以在单个事务中高效地操作多个代币。标准提供了两个函数，*balanceOfBatch*和*safeBatchTransferFrom*，使查询多个余额和转移多个代币变得更简单且燃气消耗更少。

为了符合标准的精神，我们还在非标准函数中包含了批量操作，比如*_mintBatch*。

## 构建一个ERC1155代币合约
我们将使用ERC1155来跟踪我们项目中的多个物品，每个物品都有自己独特的属性。我们将所有物品铸造给合约的部署者，然后可以将其转移到玩家手中。玩家可以自由保留他们的代币，或者根据自己的意愿与其他人进行交易，就像在区块链上处理任何其他资产一样！

为了简单起见，我们将在构造函数中铸造所有物品，但您也可以向合约添加铸造功能，以根据玩家的需求进行铸造。

> TIP
有关铸造机制的概述，请查看创建*ERC20供应*。

下面是一个代币化物品的合约示例：
```
// contracts/GameItems.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import "@openzeppelin/contracts/token/ERC1155/ERC1155.sol";

contract GameItems is ERC1155 {
    uint256 public constant GOLD = 0;
    uint256 public constant SILVER = 1;
    uint256 public constant THORS_HAMMER = 2;
    uint256 public constant SWORD = 3;
    uint256 public constant SHIELD = 4;

    constructor() public ERC1155("https://game.example/api/item/{id}.json") {
        _mint(msg.sender, GOLD, 10**18, "");
        _mint(msg.sender, SILVER, 10**27, "");
        _mint(msg.sender, THORS_HAMMER, 1, "");
        _mint(msg.sender, SWORD, 10**9, "");
        _mint(msg.sender, SHIELD, 10**9, "");
    }
}
```
请注意，对于我们的项目物品，金币是可替代的代币，而雷神之锤是不可替代的代币，因为我们只铸造了一个。

*ERC1155*合约包括可选的扩展接口*IERC1155MetadataURI*。这就是*uri*函数的来源：我们使用它来检索元数据的uri。

还请注意，与ERC20不同，ERC1155缺少小数位字段，因为每个代币都是独立的，无法分割。

部署后，我们将能够查询部署者的余额：
```
> gameItems.balanceOf(deployerAddress,3)
1000000000
```
我们可以将物品转移到玩家账户中:

```
> gameItems.safeTransferFrom(deployerAddress, playerAddress, 2, 1, "0x0")
> gameItems.balanceOf(playerAddress, 2)
1
> gameItems.balanceOf(deployerAddress, 2)
0
```

我们还可以批量转移物品到玩家账户并获取批次的余额。
```
> gameItems.safeBatchTransferFrom(deployerAddress, playerAddress, [0,1,3,4], [50,100,1,1], "0x0")
> gameItems.balanceOfBatch([playerAddress,playerAddress,playerAddress,playerAddress,playerAddress], [0,1,2,3,4])
[50,100,1,1,1]
```

可以获得元数据URI:
```
> gameItems.uri(2)
"https://game.example/api/item/{id}.json"
```

uri可以包含字符串{id}，客户端必须用实际的代币ID替换它，以小写十六进制表示（不带0x前缀），并在64个十六进制字符之前用前导零填充。

对于代币ID为2且uri为https://game.example/api/item/{id}.json，客户端将用0000000000000000000000000000000000000000000000000000000000000002替换{id}，以检索https://game.example/api/item/0000000000000000000000000000000000000000000000000000000000000002.json上的JSON。

代币ID为2的JSON文档可能如下所示：

```
{
    "name": "Thor's hammer",
    "description": "Mjölnir, the legendary hammer of the Norse god of thunder.",
    "image": "https://game.example/item-id-8u5h2m.png",
    "strength": 20
}
```
要了解有关元数据JSON模式的更多信息，请查看[ERC-1155元数据URI JSON模式](https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1155.md#erc-1155-metadata-uri-json-schema)。

> NOTE
您会注意到，物品的信息包含在元数据中，但该信息不在链上！因此，项目开发者可以更改基础元数据，从而改变项目规则！

## 向合约发送代币
使用*safeTransferFrom*时的一个关键区别是，向其他合约发送代币可能会导致以下错误消息：
```
ERC1155: transfer to non ERC1155Receiver implementer
```
这是一件好事！这意味着接收合约尚未注册自己对ERC1155协议的感知，因此禁止向其转移代币，以**防止代币永久被锁定**。例如，[Golem合约当前持有超过35万个GNT代币](https://etherscan.io/token/0xa74476443119A942dE498590Fe1f2454d7D4aC0d?a=0xa74476443119A942dE498590Fe1f2454d7D4aC0d)，价值数万美元，并且缺乏将它们取出的方法。几乎每个支持ERC20的项目都发生过这种情况，通常是由于用户错误。

为了使我们的合约能够接收ERC1155代币，我们可以继承方便的合约*ERC1155Holder*，该合约会自动处理注册过程。但是我们需要记住要实现允许代币从我们的合约中转移出去的功能。
```
// contracts/MyContract.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import "@openzeppelin/contracts/token/ERC1155/ERC1155Holder.sol";

contract MyContract is ERC1155Holder {
}
```

我们还可以使用*onERC1155Received*和*onERC1155BatchReceived*函数来实现更复杂的场景。

## 预设ERC1155合约
有一个预设的ERC1155合约可用，名为*ERC1155PresetMinterPauser*。它预设允许进行代币铸造（创建）-包括批量铸造，停止所有代币转移（暂停）并允许持有人销毁（销毁）他们的代币。该合约使用*访问控制*来控制对铸造和暂停功能的访问。部署合约的帐户将被授予铸造者和暂停者角色，以及默认的管理员角色。

这个合约可以直接部署而不需要编写任何Solidity代码。它可以用于快速原型设计和测试，也适用于生产环境。